#version 450
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_scalar_block_layout : require

// Bindless layout (set 0):
// binding BINDING_Image: storage images (HeadImage)
// binding BINDING_Buffer: storage buffers (SelectionNodes, BoxSelectResult) via indices
layout(local_size_x = 16, local_size_y = 16) in;

#include "BindlessBindings.glsl"
#include "BoxSelectPushConstants.glsl"
#include "SelectionNode.glsl"

layout(push_constant) uniform PC {
    BoxSelectPushConstants Data;
} pc;

const uint INVALID_NODE = 0xffffffffu;

layout(set = 0, binding = BINDING_Image, r32ui) uniform uimage2D HeadImages[];

layout(set = 0, binding = BINDING_Buffer, scalar) buffer SelectionNodes {
    SelectionNode Nodes[];
} SelectionBuffers[];

layout(set = 0, binding = BINDING_Buffer, scalar) buffer BoxSelectResult {
    uint Bits[];
} BoxResults[];

void main() {
    const uvec2 local = gl_GlobalInvocationID.xy;
    const uvec2 pixel = pc.Data.BoxMin + local;
    if (pixel.x >= pc.Data.BoxMax.x || pixel.y >= pc.Data.BoxMax.y) return;

    uint idx = imageLoad(HeadImages[pc.Data.HeadImageIndex], ivec2(pixel)).r;
    while (idx != INVALID_NODE) {
        const SelectionNode node = SelectionBuffers[pc.Data.SelectionNodesIndex].Nodes[idx];
        if (node.ObjectId != 0 && node.ObjectId <= pc.Data.ObjectCount) {
            const uint object_idx = node.ObjectId - 1;
            const uint word_idx = object_idx >> 5;
            const uint bit = object_idx & 31;
            atomicOr(BoxResults[pc.Data.BoxResultIndex].Bits[word_idx], 1u << bit);
        }
        idx = node.Next;
    }
}
