#version 450

layout(local_size_x = 16, local_size_y = 16) in;

layout(push_constant) uniform PC {
    uvec2 box_min;
    uvec2 box_max;
    uint object_count;
    uint _pad;
} pc;

struct SelectionNode {
    uint objectID;
    float depth;
    uint next;
};

const uint INVALID_NODE = 0xffffffffu;

layout(binding = 1, r32ui) uniform uimage2D HeadImage;

layout(binding = 2, std430) buffer SelectionNodes {
    SelectionNode nodes[];
};

layout(binding = 3, std430) buffer BoxSelectResult {
    uint bits[];
};

void main() {
    const uvec2 local = gl_GlobalInvocationID.xy;
    const uvec2 pixel = pc.box_min + local;
    if (pixel.x >= pc.box_max.x || pixel.y >= pc.box_max.y) return;

    uint idx = imageLoad(HeadImage, ivec2(pixel)).r;
    while (idx != INVALID_NODE) {
        const SelectionNode node = nodes[idx];
        if (node.objectID != 0 && node.objectID <= pc.object_count) {
            const uint object_idx = node.objectID - 1;
            const uint word_idx = object_idx >> 5;
            const uint bit = object_idx & 31;
            atomicOr(bits[word_idx], 1u << bit);
        }
        idx = node.next;
    }
}
