#version 450

layout(local_size_x = 1) in;

layout(push_constant) uniform PC {
    uvec2 target_px;
} pc;

struct SelectionNode {
    uint objectID;
    float depth;
    uint next;
};

#define MAX_CLICK_RESULTS 64
const uint INVALID_NODE = 0xffffffffu;

layout(binding = 1, r32ui) uniform uimage2D HeadImage;

layout(binding = 2, std430) buffer SelectionNodes {
    SelectionNode nodes[];
};

struct ClickHit {
    float depth;
    uint objectID;
};

layout(binding = 3, std430) buffer ClickResult {
    uint count;
    ClickHit hits[MAX_CLICK_RESULTS];
};

void main() {
    // Single invocation does the traversal
    if (gl_GlobalInvocationID.x != 0) return;

    uint idx = imageLoad(HeadImage, ivec2(pc.target_px)).r;
    while (idx != INVALID_NODE) {
        const SelectionNode node = nodes[idx];
        const uint write_idx = atomicAdd(count, 1);
        if (write_idx < MAX_CLICK_RESULTS) {
            hits[write_idx].depth = node.depth;
            hits[write_idx].objectID = node.objectID;
        }
        idx = node.next;
    }
}
